*INFORM %FF00;
*TLSEG 0 %FFFF %00040000 0 6;
*TLSEG 1 %FFFF %00080000 0 6;
*TLLOAD 1 5;
*GLOBAL 5;
*CODE 1;
*VTYPE LOGICAL64;

IMPORT VSTORE CONSOLE.INTERRUPT;
IMPORT VSTORE TELETYPE.DATA;
IMPORT VSTORE TELETYPE.CONTROL;

MODULE(PRINT.STRING, PRINT.INT, PRINT.INT64);

PSPEC PRINT.CHAR(LOGICAL8);
PSPEC PRINT.STRING(ADDR[LOGICAL8]);
PSPEC PRINT.INT(INTEGER);
PSPEC PRINT.INT64(LOGICAL64);

PROC PRINT.CHAR(C);
    %F => CONSOLE.INTERRUPT; :: Reset console interrupt bits
    0 => TELETYPE.CONTROL; :: TTY output mode
    C => TELETYPE.DATA;
    WHILE CONSOLE.INTERRUPT = 0 DO OD;
END

PROC PRINT.STRING(STRING);
    $IN I;
    FOR I < SIZE(STRING) DO
        %F => CONSOLE.INTERRUPT; :: Reset console interrupt bits
        0 => TELETYPE.CONTROL; :: TTY output mode
        STRING^[I] => TELETYPE.DATA;
        WHILE CONSOLE.INTERRUPT = 0 DO OD;
    OD
END

PROC PRINT.INT(N);
    INTEGER REM;
    IF N < 0 THEN
        PRINT.CHAR("-");
        0-N => N;
        PRINT.INT(N);
    ELSE
        IF N < 10 THEN
            PRINT.CHAR(48 + N);
        ELSE
            N - ((N / 10) * 10) => REM;
            PRINT.INT(N / 10);
            PRINT.CHAR(48 + REM);
        FI;
    FI;
END

PROC PRINT.INT64(N);
    LOGICAL64 REM;
    IF N < 10 THEN
        PRINT.CHAR(48 + N);
    ELSE
        N - ((N / 10) * 10) => REM;
        PRINT.INT64(N / 10);
        PRINT.CHAR(48 + REM);
    FI;
END

*END
